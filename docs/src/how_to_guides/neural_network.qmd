```@meta
CurrentModule = ConformalPrediction
```

# How to Conformalize a Deep Learning Image Classifier

```{julia}
#| echo: false

using Pkg; Pkg.activate("docs")
using Plots
theme(:wong)
using Random
Random.seed!(123)
```

Deep Learning is popular and --- for some tasks like image classification --- remarkably powerful. But it is also well-known that Deep Neural Networks (DNN) can be unstable [@goodfellow2014explaining] and poorly calibrated. Conformal Prediction can be used to mitigate these pitfalls. This how-to guide demonstrates how you can build an image classifier in `Flux.jl` and conformalize its predictions.

## The Task at Hand 

The task at hand is to predict the labels of handwritten images of digits using the famous MNIST dataset [@lecun1998mnist]. Importing this popular machine learning dataset in Julia is made remarkably easy through `MLDatasets.jl`:

```{julia}
using MLDatasets
N = 1000
Xraw, yraw = MNIST(split=:train)[:]
Xraw = Xraw[:,:,1:N]
yraw = yraw[1:N]
```

The chart below shows a few random samples from the training data:

```{julia}
#| output: true
#| label: fig-samples
#| fig-cap: Random samples from the MNIST dataset.

using MLJ
using Images
X = map(x -> convert2image(MNIST, x), eachslice(Xraw, dims=3))
y = coerce(yraw, Multiclass)

n_samples = 5
mosaic(rand(X, n_samples)..., ncol=n_samples)
```

## Building the Network

To model the mapping from image inputs to labels will rely on a simple Multi-Layer Perceptron (MLP). A great Julia library for Deep Learning is `Flux.jl`. But wait ... doesn't `ConformalPrediction.jl` work with models trained in `MLJ.jl`? That's right, but fortunately there exists a `Flux.jl` interface to `MLJ.jl`, namely `MLJFlux.jl`. The interface is still in its early stages, but already very powerful and easily accessible for anyone (like myself) who is used to building Neural Networks in `Flux.jl`. 

In `Flux.jl`, you could build an MLP for this task as follows,

```{julia}
using Flux

mlp = Chain(
    Flux.flatten,
    Dense(prod((28,28)), 32, relu),
    Dense(32, 10)
)
```

where `(28,28)` is just the input dimension (28x28 pixel images). Since we have ten digits, our output dimension is ten.^[For a full tutorial on how to build an MNIST image classifier relying solely on `Flux.jl`, check out this [tutorial](https://fluxml.ai/Flux.jl/stable/tutorials/2021-01-26-mlp/).]

We can do the exact same thing in `MLJFlux.jl` as follows,

```{julia}
using MLJFlux

builder = MLJFlux.@builder Chain(
    Flux.flatten,
    Dense(prod(n_in), 32, relu),
    Dense(32, n_out)
)
```

where here we rely on the `@builder` macro to make the transition from `Flux.jl` to `MLJ.jl` as seamless as possible. Finally, `MLJFlux.jl` already comes with a number of helper functions to define plain-vanilla networks. In this case, we will use the `ImageClassifier` with our custom builder and cross-entropy loss:

```{julia}
ImageClassifier = @load ImageClassifier
clf = ImageClassifier(
    builder=builder,
    epochs=10,
    loss=Flux.crossentropy
)
```

The generated instance `clf` is a model (in the `MLJ.jl` sense) so from this point on we can rely on standard `MLJ.jl` workflows. For example, we can wrap our model in data to create a machine and then evaluate it on a holdout set as follows:

```{julia}
mach = machine(clf, X, y)

evaluate!(
    mach,
    resampling=Holdout(rng=123, fraction_train=0.8),
    operation=predict_mode,
    measure=[accuracy]
)
```

The accuracy of our very simple model is not amazing, but good enough for the purpose of this tutorial. For each image, our MLP returns a softmax output for each possible digit: 0,1,2,3,...,9. Since each individual softmax output is valued between zero and one, $y_k\in(0,1)$, this is commonly interpreted as a probability: $y_k\coloneqq p(y=k|X)$. Edge cases -- that is values close to either zero or one - indicate high predictive certainty. But this is only a heuristic notion of predictive uncertainty [@angelopoulos2021gentle]. Next, we will turn this heuristic notion of uncertainty into a rigorous one using Conformal Prediction.

## Conformalizing the Network

Since `clf` is a model, it is also compatible with our package: `ConformalPrediction.jl`. To conformalize our MLP, we therefore only need to call `conformal_model(clf)`. Since the generated instance `conf_model` is also just a model, we can still rely on standard `MLJ.jl` workflows. Below we first wrap it in data and then fit it. Aaaand ... we're done! Let's look at the results in the next section.

```{julia}
using ConformalPrediction
conf_model = conformal_model(clf)
mach = machine(conf_model, X, y)
fit!(mach)
```

## Results

```{julia}
#| echo: false

using Plots
using StatsBase: sample

function plot_results(mach, X, y; set_size=1, n_samples=3)

    # Choose images:
    set_sizes = ConformalPrediction.set_size.(predict(mach, X))
    candidates = findall(set_sizes .== set_size)
    @assert length(candidates) > 0 "No sets of size $set_size."
    chosen = sample(candidates, n_samples, replace=false)

    plt_lst = []
    for i in chosen
        ytrue = y[i]
        x = X[i]
        ŷ = predict(mach, x)[1]
        title = join(["$(Int(key)-1) ($(round(val, digits=2)))" for (key, val) in ŷ.prob_given_ref], ", ")
        title = "C={$title}\nytrue=$ytrue"
        plt = plot(x, axis=false)
        annotate!(plt, (14, 31, (title, 10-set_size)))
        push!(plt_lst, plt)
    end

    plot(plt_lst..., size=(n_samples*200, 200), layout=(1,n_samples))
end
```

The charts below present the results. The first row displays highly certain predictions, now defined in the rigorous sense of Conformal Prediction: in each case, the conformal set (just beneath the image) includes only one label. The following two rows display increasingly uncertain predictions of set size two and three, respectively. For example, the conformal set for the center image in the bottom row includes both $y=1$ and $y=2$, which seems plausible: there genuinely is some ambiguity even from the perspective of a human inspector.

```{julia}
#| output: true
#| label: fig-plots
#| fig-cap: Conformalised predictions from an image classifier.
#| fig-subcap:
#|   - "Plot 1"
#|   - "Plot 2"
#|   - "Plot 3"
#| layout-nrow: 3

display(plot_results(mach, X, y; set_size=1))
display(plot_results(mach, X, y; set_size=2))
display(plot_results(mach, X, y; set_size=3))
```

As always, we can also evaluate our conformal model in terms of coverage (correctness) and conditional coverage (adaptiveness).

```{julia}
_eval = evaluate!(
    mach,
    resampling=Holdout(rng=123, fraction_train=0.8),
    operation=predict,
    measure=[emp_coverage, ssc]
)
println("Empirical coverage: $(round(_eval.measurement[1], digits=3))")
println("SSC: $(round(_eval.measurement[2], digits=3))")
```

# References